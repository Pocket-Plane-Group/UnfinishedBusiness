///////////////////////
///////////////////////
// Item Restorations //
///////////////////////
///////////////////////

/* Gloves of Missile Snaring */
COPY_EXISTING "brac18.itm" "override"
  WRITE_LONG 0x001e 0	// Remove undocumented unusabilities

/* Chateau Irenicus, put Gloves on shadow thief assassin. */
COPY_EXISTING "ishadft1.cre" "override"
  ADD_CRE_ITEM "brac18" #0 #0 #0 "none" "gloves"

/* Remove Gloves from unfought shadow thief. */
COPY_EXISTING "trthf01.cre" "override"
  REMOVE_CRE_ITEM "brac18"


/* Shazzellim */
COPY_EXISTING sw1h50.itm  override
  READ_LONG  0x1e unusability
  WRITE_LONG 0x1e unusability | 0x04  /* Restrict Good-aligned */
  SAY IDENTIFIED_DESC @33             /* Altered description for Shazzellim */
BUT_ONLY

/* Prebek's Home, put Shazzellim in container #2 */
COPY_EXISTING ar0407.are override
  LAUNCH_PATCH_FUNCTION ADD_AREA_ITEM
    INT_VAR
    container_to_add_to	= 2
    STR_VAR
    item_to_add		= "sw1h50"
  END
BUT_ONLY

/* Remove Elhan's evil-aligned Shazzellim scimitars */
COPY_EXISTING c6elhan.cre override
              suelhan.cre override
  REPLACE_CRE_ITEM sw1h22 #0 #0 #0 NONE        WEAPON1 EQUIP
  REPLACE_CRE_ITEM sw1h22 #0 #0 #0 NONE        SHIELD
  REPLACE_CRE_ITEM amul21 #0 #0 #0 UNSTEALABLE AMULET
IF sw1h50
BUT_ONLY


/* Wolfskin Bag */
COPY_EXISTING bag01.itm override
  READ_LONG  0x18 flags
  WRITE_LONG 0x18 flags | 0x01
  SAY NAME1 @31
  SAY NAME2 @31
  SAY UNIDENTIFIED_DESC @32
  SAY IDENTIFIED_DESC   @32
BUT_ONLY

/* Wolfskin Bag - related .sto file */
COPY_EXISTING bag01.sto override
  WRITE_SHORT 0x22 0x0a          /* Change bag capacity from 5 to 10 */
  SAY STORE_NAME @31
BUT_ONLY

/* The Copper Coronet, places the Wolfskin Bag on the Beastmaster */
COPY_EXISTING beast.cre override
  ADD_CRE_ITEM bag01 #0 #0 #0 NONE INV1


/* Wraithform */
COPY ~%MOD_FOLDER%/items/spwi315.spl~  override /* Wraithform spell file */
  SAY NAME1 #12020
  SAY UNIDENTIFIED_DESC #12144

ACTION_IF NOT FILE_EXISTS_IN_GAME ~scrl1r.itm~
BEGIN
  COPY ~%MOD_FOLDER%/items/scrl1r.itm~ override /* Wraithform scroll item file */
  SAY NAME2 #13065
  SAY IDENTIFIED_DESC   #12144
END

COPY_EXISTING scrolls.sto override // The Pen and Parchment Store
  ADD_STORE_ITEM ~scrl1r~ BEFORE scrl1t #1 #0 #0 IDENTIFIED #5

/* Boots of the West */
COPY ~%MOD_FOLDER%/items/misc8j.itm~ override
  SAY NAME1             #43877
  SAY NAME2             #43877
  SAY UNIDENTIFIED_DESC #43878
  SAY IDENTIFIED_DESC   #43878

/* Umar Hills, places the Boots of the West on Derrick */
COPY_EXISTING valran01.cre override
  ADD_CRE_ITEM misc8j #0 #0 #0 NONE BOOTS


// Ranger Cabin, put Taragarth +1 in container #6
COPY_EXISTING "ar1102.are" "override"
  LAUNCH_PATCH_FUNCTION ADD_AREA_ITEM
    INT_VAR
    container_to_add_to	= 6
    STR_VAR
    item_to_add		= "sw1h37"
  END


/* Boots of Hastened Departure */
COPY_EXISTING boot09.itm override
  SAY NAME2           @97
  SAY IDENTIFIED_DESC @98
  GET_OFFSET_ARRAY hd_array ITM_V10_HEADERS
  PHP_EACH hd_array AS int => hd_offset
  BEGIN
    READ_ASCII hd_offset + 0x0004 icon
    PATCH_IF ~%icon%~ STRING_EQUAL ~~ || ~%icon%~ STRING_EQUAL_CASE ~none~
    BEGIN
      WRITE_ASCII hd_offset + 0x0004 iboot09 #8
    END
  END
BUT_ONLY

/* Adds the Boots of Hastened Departure to Greyhand */
ACTION_IF FILE_EXISTS_IN_GAME greyhand.cre THEN BEGIN
  COPY_EXISTING greyhand.cre override
    REPLACE_CRE_ITEM boot09 #0 #0 #0 NONE BOOTS
  BUT_ONLY
END

// Third journal of Irenicus
// by Angel

// Proper text
COPY_EXISTING "misc9k.itm" "override"
  SAY UNIDENTIFIED_DESC @1312

// Description bam is way off center,
// provide a properly centered one.
COPY_LARGE "ub/items/cmisc9k.bam" "override"

// Add to other journals on desk.
COPY_EXISTING "ar1515.are" "override"
  LAUNCH_PATCH_FUNCTION ADD_AREA_ITEM
    INT_VAR
    container_to_add_to	= 5
    STR_VAR
    item_to_add		= "misc9k"
  END

// Remove extra copy of first journal.
COPY_EXISTING "ar1516.are" "override"
  LAUNCH_PATCH_FUNCTION DELETE_AREA_ITEM
    STR_VAR
    item_to_delete	= "misc9i"
  END


// Restore import02.2da on non-EE games

ACTION_IF ENGINE_IS "bg2 tob"
BEGIN
  // Sanitized import list with items not already in BG2.
  COPY "%MOD_FOLDER%/items/import02.2da" "override"

  // Restore proper name and description to Krotan's Skullcrusher
  COPY_EXISTING "blun11.itm" "override"
    SAY NAME2 ~Krotan's Skullcrusher~
    SAY IDENTIFIED_DESC ~Mace +2: "Krotan's Skullcrusher"

Even the hordes have their heroes, and while they are vilified in civilized company, their deeds are sometimes impressive nonetheless.  Krotan was an exceptionally intelligent orc, as far as orcs go, who commanded an ever-shifting legion of ogres, orcs, hobgoblins, and other assorted beasties during the Year of the Lost Lady, 1241DR. It was in this year that a group of orc bandits allegedly kidnapped and killed a Tethyrian noblewoman, and the response from authorities was a genocidal slaughter of orcs throughout the south in her memory. Krotan mounted a defense that ensured his people were not entirely destroyed; though they were displaced. Krotan himself fell near the end of the conflict, and his head was mounted outside the graveyard where the young lady was buried. As a point of interest, orcs called this time the "Year of Pushing Too Far."

STATISTICS:

Damage: 1D6 + 3
Damage Type: Crushing
THAC0: +2
Weight: 7
Speed Factor: 5
Proficiency Type: Blunt Weapons
Type: 1-handed
Not Usable by:
 Druid
 Mage
 Thief~

  // Add import02 to Chateau Irenicus
  COPY_EXISTING "ar0602.bcs" "override"
    DECOMPILE_AND_PATCH
    BEGIN
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH
       ~ActionOverride("Shelf1",TakeItemListPartyNum("IMPORT03",1))~
       ~ActionOverride("Shelf1",TakeItemListPartyNum("IMPORT03",1))
        ActionOverride("Table3",TakeItemListPartyNum("IMPORT02",1))~
    END
END


