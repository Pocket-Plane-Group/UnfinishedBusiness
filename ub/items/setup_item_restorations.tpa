DEFINE_ACTION_FUNCTION setup_item_restorations
BEGIN
  ///////////////////////
  ///////////////////////
  // Item Restorations //
  ///////////////////////
  ///////////////////////

  /* Gloves of Missile Snaring */
  COPY_EXISTING "brac18.itm" "override"
    WRITE_LONG 0x001e 0	// Remove undocumented unusabilities

  /* Chateau Irenicus, put Gloves on shadow thief assassin. */
  COPY_EXISTING "ishadft1.cre" "override"
    ADD_CRE_ITEM "brac18" #0 #0 #0 "none" "gloves"

  /* Remove Gloves from unfought shadow thief. */
  COPY_EXISTING "trthf01.cre" "override"
    REMOVE_CRE_ITEM "brac18"


  /* Shazzellim */

  /* QuestPack has a similar component in Alternative Harpers/Xzar Plot,	*/
  /* so making sure not to do this twice.					*/

  ACTION_IF NOT MOD_IS_INSTALLED "setup-d0questpack" 2
  BEGIN
    COPY_EXISTING sw1h50.itm  override
      WRITE_LONG 0x1e (THIS | BIT2)  /* Restrict Good-aligned */
      SAY IDENTIFIED_DESC @33             /* Altered description for Shazzellim */
    BUT_ONLY

    /* Prebek's Home, put Shazzellim in container #2 */
    COPY_EXISTING ar0407.are override
      LAUNCH_PATCH_FUNCTION ADD_AREA_ITEM
        INT_VAR
        container_to_add_to	= 2
        STR_VAR
        item_to_add		= "sw1h50"
      END
      BUT_ONLY

    /* Remove Elhan's evil-aligned Shazzellim scimitars */
    COPY_EXISTING c6elhan.cre override
                  suelhan.cre override
      REPLACE_CRE_ITEM sw1h22 #0 #0 #0 NONE        WEAPON1 EQUIP
      REPLACE_CRE_ITEM sw1h22 #0 #0 #0 NONE        SHIELD
      REPLACE_CRE_ITEM amul21 #0 #0 #0 UNSTEALABLE AMULET
    IF sw1h50
    BUT_ONLY
  END	// NOT MOD_IS_INSTALLED "setup-d0questpack" 2


  /* Wolfskin Bag */
  COPY_EXISTING bag01.itm override
    READ_LONG  0x18 flags
    WRITE_LONG 0x18 flags | 0x01
    SAY NAME1 @31
    SAY NAME2 @31
    SAY UNIDENTIFIED_DESC @32
    SAY IDENTIFIED_DESC   @32
  BUT_ONLY

  /* Wolfskin Bag - related .sto file */
  COPY_EXISTING bag01.sto override
    WRITE_SHORT 0x22 0x0a          /* Change bag capacity from 5 to 10 */
    SAY STORE_NAME @31
  BUT_ONLY

  /* The Copper Coronet, places the Wolfskin Bag on the Beastmaster */
  COPY_EXISTING beast.cre override
    ADD_CRE_ITEM bag01 #0 #0 #0 NONE INV1


  /* Wraithform */
  COPY ~%MOD_FOLDER%/items/spwi315.spl~  override /* Wraithform spell file */
    SAY NAME1 #12020
    SAY UNIDENTIFIED_DESC #12144

  ACTION_IF NOT FILE_EXISTS_IN_GAME ~scrl1r.itm~
  BEGIN
    COPY ~%MOD_FOLDER%/items/scrl1r.itm~ override /* Wraithform scroll item file */
    SAY NAME2 #13065
    SAY IDENTIFIED_DESC   #12144
  END

  COPY_EXISTING scrolls.sto override // The Pen and Parchment Store
    ADD_STORE_ITEM ~scrl1r~ BEFORE scrl1t #1 #0 #0 IDENTIFIED #5

  /* Boots of the West */
  COPY ~%MOD_FOLDER%/items/misc8j.itm~ override
    SAY NAME1             #43877
    SAY NAME2             #43877
    SAY UNIDENTIFIED_DESC #43878
    SAY IDENTIFIED_DESC   #43878

  /* Umar Hills, places the Boots of the West on Derrick */
  COPY_EXISTING valran01.cre override
    ADD_CRE_ITEM misc8j #0 #0 #0 NONE BOOTS


  // Ranger Cabin, put Taragarth +1 in container #6
  COPY_EXISTING "ar1102.are" "override"
    LAUNCH_PATCH_FUNCTION ADD_AREA_ITEM
      INT_VAR
      container_to_add_to	= 6
      STR_VAR
      item_to_add		= "sw1h37"
    END


  /* Boots of Hastened Departure */
  COPY_EXISTING boot09.itm override
    SAY NAME2           @97
    SAY IDENTIFIED_DESC @98
    GET_OFFSET_ARRAY hd_array ITM_V10_HEADERS
    PHP_EACH hd_array AS int => hd_offset
    BEGIN
      READ_ASCII hd_offset + 0x0004 icon
      PATCH_IF ~%icon%~ STRING_EQUAL ~~ || ~%icon%~ STRING_EQUAL_CASE ~none~
      BEGIN
        WRITE_ASCII hd_offset + 0x0004 iboot09 #8
      END
    END
  BUT_ONLY

  /* Adds the Boots of Hastened Departure to Greyhand */
  ACTION_IF FILE_EXISTS_IN_GAME greyhand.cre THEN BEGIN
    COPY_EXISTING greyhand.cre override
      REPLACE_CRE_ITEM boot09 #0 #0 #0 NONE BOOTS
    BUT_ONLY
  END

  // Third journal of Irenicus
  // by Angel

  // Proper text
  COPY_EXISTING "misc9k.itm" "override"
    SAY UNIDENTIFIED_DESC @1312

  // Description bam is way off center,
  // provide a properly centered one.
  COPY_LARGE "ub/items/cmisc9k.bam" "override"

  // Add to other journals on desk.
  COPY_EXISTING "ar1515.are" "override"
    LAUNCH_PATCH_FUNCTION ADD_AREA_ITEM
      INT_VAR
      container_to_add_to	= 5
      STR_VAR
      item_to_add		= "misc9k"
    END

  // Remove extra copy of first journal.
  COPY_EXISTING "ar1516.are" "override"
    LAUNCH_PATCH_FUNCTION DELETE_AREA_ITEM
      STR_VAR
      item_to_delete	= "misc9i"
    END


  // Old engine only:
  // - Restore unique BG1 +2 weapon names/descriptions.
  // - Restore import02.2da in Chateau Irenicus (EE already does this).

  ACTION_IF ENGINE_IS "bg2 tob"
  BEGIN
    // Restore BG1 names and descriptions of repurposed items.

    COPY_EXISTING "ax1h03.itm" "override"
      SAY NAME2 @3000
      SAY IDENTIFIED_DESC @3001

    COPY_EXISTING "blun11.itm" "override"
      SAY NAME2 @3002
      SAY IDENTIFIED_DESC @3003

    COPY_EXISTING "halb03.itm" "override"
      SAY NAME2 @3004
      SAY IDENTIFIED_DESC @3005

    COPY_EXISTING "plat05.itm" "override"
      SAY NAME2 @3006
      SAY IDENTIFIED_DESC @3007

    COPY_EXISTING "sw1h06.itm" "override"
      SAY NAME2 ~Varscona~ // No need to translate


    // Sanitized import list with items not already in BG2.

    COPY "%MOD_FOLDER%/items/import02.2da" "override"


    // Add import02 to Chateau Irenicus

    COPY_EXISTING "ar0602.bcs" "override"
      DECOMPILE_AND_PATCH
      BEGIN
        REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH
         ~ActionOverride("Shelf1",TakeItemListPartyNum("IMPORT03",1))~
         ~ActionOverride("Shelf1",TakeItemListPartyNum("IMPORT03",1))
          ActionOverride("Table3",TakeItemListPartyNum("IMPORT02",1))~
      END
  END


  /* Plate of the Dark */

  // Put on Tazok in Firkraag's lair
  COPY_EXISTING "tazok.cre" "override"
    REPLACE_CRE_ITEM "plat05" #0 #0 #0 "unstealable" "armor"
    BUT_ONLY_IF_IT_CHANGES


  /* The Practical Defense */

  // Restore BG1 name/desc (yes, even in EE)

  COPY_EXISTING "plat08.itm" "override"
    SAY NAME2 @3008
    SAY IDENTIFIED_DESC @3009

  // Add to Trademeet nobleman merchant's inventory.
  COPY_EXISTING "trmer02.sto" "override"
    ADD_STORE_ITEM "plat08" AFTER "plat10" #0 #0 #0 "identified" #1


  /* The Whistling Sword */

  // This one is already found in AR0303, Maelar's guildhouse.
  // All we have to do is eliminate the multiple copies, see below.

  // Restore BG1 name/desc (yes, even in EE)

  COPY_EXISTING "sw1h09.itm" "override"
    SAY NAME2 @3010
    SAY IDENTIFIED_DESC @3011


  // Replace duplicate copies of BG1 unique items with generic versions.

  COPY_EXISTING "ar2210.are" "override"
    LAUNCH_PATCH_FUNCTION REPLACE_AREA_ITEM
      STR_VAR
      old_item	= "sw1h09"
      new_item	= "sw1h29"
    END

    BUT_ONLY_IF_IT_CHANGES


  COPY_EXISTING "deckass1.cre"	"override"
		"calaha.cre"	"override"
		"sarkis02.cre"	"override"
		"sarkis03.cre"	"override"
		"sarcpt01.cre"	"override"
		"vvcat.cre"	"override"
		"hgskl02.cre"	"override"
		"c6kach.cre"	"override"
		"sargrd05.cre"	"override"
		"sargrd09.cre"	"override"
		"arkanisg.cre"	"override"
                "vvarkan.cre"	"override"
		"hlketta.cre"	"override"
		"hlketta2.cre"	"override"
		"teltief2.cre"	"override"
		"ammerc07.cre"	"override"
		"gordeck6.cre"	"override"
		"gordeck7.cre"	"override"
		"gordeck8.cre"	"override"
		"gordeck9.cre"	"override"
		"gordecka.cre"	"override"
    REPLACE_CRE_ITEM "sw1h29" #0 #0 #0 "none" "weapon1" EQUIP
    BUT_ONLY_IF_IT_CHANGES


  COPY_EXISTING "ysfire02.cre"	"override"
    REPLACE_CRE_ITEM "ax1h11" #0 #0 #0 "none" "weapon1" EQUIP
    BUT_ONLY_IF_IT_CHANGES


  COPY_EXISTING "senorc02.cre"	"override"
                "senorc04.cre"	"override"
		"ammerc08.cre"	"override"
		"ammerc09.cre"	"override"
    REPLACE_CRE_ITEM "sw1h29" #0 #0 #0 "none" "weapon2"
    BUT_ONLY_IF_IT_CHANGES


  COPY_EXISTING "sengua04.cre"	"override"
    REPLACE_CRE_ITEM "ax1h11" #0 #0 #0 "none" "weapon1" EQUIP
    REPLACE_CRE_ITEM "plat14" #0 #0 #0 "none" "armor"
    BUT_ONLY_IF_IT_CHANGES


  COPY_EXISTING "deckwar1.cre"	"override"
    REPLACE_CRE_ITEM "sw1h29" #0 #0 #0 "undroppable" "weapon1" EQUIP
    REPLACE_CRE_ITEM "sw1h29" #0 #0 #0 "undroppable" "shield" EQUIP
    REPLACE_CRE_ITEM "plat14" #0 #0 #0 "undroppable" "armor"
    BUT_ONLY_IF_IT_CHANGES


  COPY_EXISTING "bazliz03.cre"	"override"
                "bazliz04.cre"	"override"
    REPLACE_CRE_ITEM "plat14" #0 #0 #0 "none" "armor"
    BUT_ONLY_IF_IT_CHANGES
END	// setup_item_restorations


